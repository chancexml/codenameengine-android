package funkin.backend.assets;

import openfl.utils.AssetLibrary;
import lime.media.AudioBuffer;
import lime.graphics.Image;
import lime.text.Font;
import lime.utils.Bytes;
import haxe.io.Path;

#if MOD_SUPPORT
import sys.FileStat;
import sys.FileSystem;
#end

using StringTools;

class ModsFolderLibrary extends AssetLibrary implements IModsAssetLibrary {
	public var basePath:String;
	public var modName:String;
	public var libName:String;
	public var prefix = 'assets/';

	public function new(basePath:String, libName:String, ?modName:String) {
		this.basePath = resolveAndroidPath(basePath);
		this.libName = libName;
		this.prefix = 'assets/';
		this.modName = modName == null ? libName : modName;
		super();
	}

	private function resolveAndroidPath(path:String):String {
		#if android
		if (path == null) return null;
		var p = Path.normalize(path);
		
		if (p == "mods" || p.startsWith("mods/")) {
			var remainder = p.length > 4 ? p.substr(5) : "";
			return Path.normalize(Path.join(["/storage/emulated/0/Android/media/com.yoshman29.codenameengine/files/", remainder]));
		} else if (p == "assets" || p.startsWith("assets/")) {
			var remainder = p.length > 6 ? p.substr(7) : "";
			return Path.normalize(Path.join(["/storage/emulated/0/Android/data/com.yoshman29.codenameengine/files/", remainder]));
		}
		#end
		return path;
	}

	function toString():String {
		return '(ModsFolderLibrary: $modName)';
	}

	#if MOD_SUPPORT
	private var editedTimes:Map<String, Float> = [];
	public var _parsedAsset:String = null;

	public function getEditedTime(asset:String):Null<Float> {
		return editedTimes[asset];
	}

	public override function getAudioBuffer(id:String):AudioBuffer {
		if (!exists(id, "SOUND")) return null;
		var path = getAssetPath();
		editedTimes[id] = FileSystem.stat(path).mtime.getTime();
		return AudioBuffer.fromFile(path);
	}

	public override function getBytes(id:String):Bytes {
		if (!exists(id, "BINARY")) return null;
		var path = getAssetPath();
		editedTimes[id] = FileSystem.stat(path).mtime.getTime();
		return Bytes.fromFile(path);
	}

	public override function getFont(id:String):Font {
		if (!exists(id, "FONT")) return null;
		var path = getAssetPath();
		editedTimes[id] = FileSystem.stat(path).mtime.getTime();
		return ModsFolder.registerFont(Font.fromFile(path));
	}

	public override function getImage(id:String):Image {
		if (!exists(id, "IMAGE")) return null;
		var path = getAssetPath();
		editedTimes[id] = FileSystem.stat(path).mtime.getTime();
		return Image.fromFile(path);
	}

	public override function getPath(id:String):String {
		if (!__parseAsset(id)) return null;
		return getAssetPath();
	}

	public inline function getFolders(folder:String):Array<String>
		return __getFiles(folder, true);

	public inline function getFiles(folder:String):Array<String>
		return __getFiles(folder, false);

	public function __getFiles(folder:String, folders:Bool = false) {
		if (!folder.endsWith("/")) folder += "/";
		if (!__parseAsset(folder)) return [];
		var path = getAssetPath();
		try {
			var result:Array<String> = [];
			for(e in FileSystem.readDirectory(path))
				if (FileSystem.isDirectory(Path.join([path, e])) == folders)
					result.push(e);
			return result;
		} catch(e) {}
		return [];
	}

	public override function exists(asset:String, type:String):Bool {
		if(!__parseAsset(asset)) return false;
		return FileSystem.exists(getAssetPath());
	}

	private function __isCacheValid(cache:Map<String, Dynamic>, asset:String, isLocalCache:Bool = false) {
		if (!editedTimes.exists(asset)) return false;
		var editedTime = editedTimes[asset];
		if (editedTime == null || editedTime < FileSystem.stat(getPath(asset)).mtime.getTime()) return false;
		if (!isLocalCache) asset = '$libName:$asset';
		return cache.exists(asset) && cache[asset] != null;
	}

	private function __parseAsset(asset:String):Bool {
		if (asset == null || !asset.startsWith(prefix)) return false;
		
		_parsedAsset = asset.substr(prefix.length);
		
		if(ModsFolder.useLibFile) {
			var file = new Path(_parsedAsset);
			if(file.file.startsWith("LIB_")) {
				var library = file.file.substr(4);
				if(library != modName) return false;
				
				var dir = (file.dir != null && file.dir != "") ? file.dir + "/" : "";
				_parsedAsset = dir + file.file + (file.ext != null ? "." + file.ext : "");
			}
		}
		return true;
	}

	public override function list(type:String):Array<String> {
		var result = [];
		__listAppend(result, '');
		return result;
	}

	function __listAppend(arr:Array<String>, folder:String) {
		var fullFolderPath = Path.join([basePath, folder]);
		if (!FileSystem.exists(fullFolderPath)) return;
		
		for(file in FileSystem.readDirectory(fullFolderPath)) {
			var fullPath = Path.join([fullFolderPath, file]);
			if (FileSystem.isDirectory(fullPath))
				__listAppend(arr, folder + file + '/');
			else
				arr.push(prefix + folder + file);
		}
	}
	#end

	public function getAssetPath():String {
		var p = Path.normalize(Path.join([basePath, _parsedAsset]));
		return p;
	}

	@:noCompletion public var folderPath(get, set):String;
	@:noCompletion private inline function get_folderPath():String { return basePath; }
	@:noCompletion private inline function set_folderPath(value:String):String { return basePath = resolveAndroidPath(value); }
}
